0010 REM empl_rpt01.bbx - Employee Listing
2000 REM -----<FILE IOLIST
2010 IOLIST A0$,A1$,A2$,A3$,A4$
2020 rem EMPL_ID:C(6*=10),NAME_LAST:C(50*=10),NAME_FIRST:C(50*=10),SAL_HOUR:C(1*=10),DEPT:C(20*=10)
2030 GOTO 30000
8000 REM Run Barista query
8010 bbjHome$=System.getProperty("basis.BBjHome")
8030 pid$=hta(info(3,0));ns_name$="query_result"+pid$
8040 a=scall(" bbj -tT0 -q -WD"""+bbjHome$+"/barista/"" -c"""+bbjHome$+
8040:   "/barista/sys/config/enu/barista.cfg"" sys/prog/bax_launch_task.bbj "+
8040:   "- -yQ -uguest -p -qMOD_QUERY -n"+ns_name$+" -w")
8050 selectedKey$=cast(BBjString,BBjAPI().getGroupNamespace().getValue(ns_name$,err=*next))
8060 if pos("^"=selectedKey$,-1)=len(selectedKey$) then selectedKey$=selectedKey$(1,len(selectedKey$)-1)
8070 KEY1$=selectedKey$
8080 if pos("CANCEL"=key1$)<>0 then key1$=""
8090 return
30000 REM -----<EMPLOYEE LISTING
30010 PRINT @(0,22),'RB','CL',@(5,22),"From Employee ID: ",
30020 CALL BASEDIR$+"prog/std_input.bbx",23,22,0,6,0,0,1,4,"",KEY1$,ROUTE$,"","",HELPID$,""
30030 IF ROUTE$="F3" THEN GOSUB 8000
30040 LET A0$="",A1$="",A2$="";READ(FILE1,KEY=KEY1$,KNUM=ALT,DOM=30041)IOL=2010
30050 PRINT @(23,22),KEY1$," ",A1$," ",A2$
30060 HDG_FROM$=KEY1$;IF HDG_FROM$="" THEN LET HDG_FROM$="FIRST"
30070 LET BEG_EMP$=KEY1$
30100 PRINT @(0,23),'CL',@(7,23),"To Employee ID: ",
30110 CALL BASEDIR$+"prog/std_input.bbx",23,23,0,6,0,0,1,4,"",KEY1$,ROUTE$,"","",HELPID$,""
30120 IF ROUTE$="F3" THEN GOSUB 8000
30130 LET A0$="",A1$="",A2$="";READ(FILE1,KEY=KEY1$,KNUM=ALT,DOM=30131)IOL=2010
30140 PRINT @(23,23),KEY1$," ",A1$," ",A2$
30150 HDG_TO$=KEY1$;IF HDG_TO$="" THEN LET HDG_TO$="LAST"
30160 LET END_EMP$=KEY1$
30170 IF END_EMP$<>"" THEN IF END_EMP$<BEG_EMP$ THEN GOTO 30100
30180 LET LNCNT=51,MAXLNS=50
40000 REM -----<OPEN PRINTER
40010 PRT_DEV=UNT;OPEN (PRT_DEV)"PD"
40050 REM -----<MAIN PROCESS LOOP
40100 READ(FILE1,KEY=BEG_EMP$,DIR=0,DOM=40101)
40110 READ(FILE1,END=41000)IOL=2010
40120 IF END_EMP$<>"" THEN IF A0$>END_EMP$ THEN GOTO 41000
40130 IF LNCNT>MAXLNS THEN GOSUB 40200
40140 PRINT (PRT_DEV)@(5),A0$,@(15),CVS(A1$,2)+", "+A2$,@(44),A3$,@(55),A4$
40150 LET LNCNT=LNCNT+1
40160 LET RECCNT=RECCNT+1
40170 GOTO 40110
40200 REM -----<REPORT HEADINGS
40210 IF PG>0 THEN PRINT (PRT_DEV)'FF' ELSE PRINT (PRT_DEV)'LF',
40220 PG=PG+1
40230 PRINT (PRT_DEV)'LF',@(5),"Date: ",DAY,'LF',@(5),"Time: ",DATE(0:"%h:%m %p"),@(30),"Employee Listing",@(70),"Page: ",STR(PG)
40240 PRINT (PRT_DEV)'LF',@(5),"From: ",HDG_FROM$,'LF',@(7),"To: ",HDG_TO$,'LF','LF'
40250 PRINT (PRT_DEV)'LF',@(42),"Salary/"
40260 PRINT (PRT_DEV)@(5),"Emp ID",@(15),"Employee Name",@(42),"Hourly",@(55),"Department",'LF'
40270 LET LNCNT=8
40290 RETURN
41000 PRINT (PRT_DEV)'LF',@(5),"Total records: ",STR(RECCNT)
41010 CLOSE (PRT_DEV)
42000 RUN BASEDIR$+"prog/empl_fm01.bbx"







